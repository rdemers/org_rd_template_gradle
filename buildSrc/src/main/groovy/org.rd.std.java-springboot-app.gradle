//
// R. Demers; 2021.
// Springboot application conventions.
//
plugins {
    // Apply the common convention plugin for shared build configuration.
    id "org.rd.std.java-common"

    // Springboot plugin.
    id "org.springframework.boot"
}

// Project description.
description = """
----------------------------------------------------------------
Gradle example project.
Project name: ${project.name}

Put more details here ... 
----------------------------------------------------------------
"""

// Extra variables definitions.
ext {
    springBootVersion = "2.4.2"
}

// Dependencies : caution, WEB activated. We assume MSA.
dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    developmentOnly     "org.springframework.boot:spring-boot-devtools"

    implementation      "org.springframework.boot:spring-boot-starter-actuator"
    implementation      "org.springframework.boot:spring-boot-starter-web"

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"

    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage:junit-vintage-engine"
    }
}

// Springboot extension.
configurations {
    // Only need is you activate : implementation gradleApi().
    // See org.rd.std.java-common.gradle for more information.
    // all*.exclude module : "spring-boot-starter-logging"
    compileOnly {
        extendsFrom annotationProcessor
    }
}

// Layers description.
bootJar {
    layered {
        enabled = true
        application {
            intoLayer("spring-boot-loader") {
                include "org/springframework/boot/loader/**"
            }
            intoLayer("application")
        }
        dependencies {
            intoLayer("snapshot-dependencies") {
                include "*:*:*SNAPSHOT"
            }
            intoLayer("dependencies")
        }
        layerOrder = ["dependencies", "spring-boot-loader", "snapshot-dependencies", "application"]
    }
}